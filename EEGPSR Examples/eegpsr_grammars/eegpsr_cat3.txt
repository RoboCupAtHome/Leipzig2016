##############################################################################
#
# Eegpsr_cat3 Grammar
# 
# Author:  Mauricio Matamoros
# Date:    June 2016
#
# IMPORTANT: 
# Since this document declares a grammar for generating sentences,
# NO RECURSIVE rules must be used.
# 
##############################################################################

/*****************************************************************************
*
* FORMAT
*
******************************************************************************
*
* ROOT
* The root rule S of the grammar is the $Main Non-Terminal. In order for the
* grammar to be valid, it must contain at least one $Main rule.
*
* Non-Terminal identifiers
* A valid Non-Terminal identifier is a sequence of one or more letters,
* digits, or underscore characters (_). Spaces, punctuation marks, and symbols
* cannot be part of a Non-Terminal identifier.
*
* Wildcards
* Wilcards are strings within braces which are used during random sentence
* generation, replacing them by random values. The same random value may be
* specified for a set of wildcards by specifying an ID. Also, the type may
* be optionally specified. Syntax is as follows:
*
*	{wildcard [type] [id] [meta:[metadata]]}
*
* When the "meta:" literal string is found within a wildcard, all text
* following it is addressed as metadata. While etadata is not part of the 
* generated sentence, it can be used to display additional information.
*
* The following wildcards are defined:
*	{category}	An object category.
*				Types are ignored.
*	{gesture}	A gesture name.
*				Types are ignored.
*	{location}	Any location.
*				Types may be [room|placement|beacon]
*	{name}		A person name.
*				Types may be [male|female]
*	{object}	An object name.
*				Types may be [known|alike]
*	{question}	The "question" literal string.
*				Types are ignored. Metadata contains a question/answer
*				from the predefined questions set.
*	{void}		Void construct used to inject metadata.
*
*				Types are ignored.
* Also, the following alias are defined
*	{beacon}	Alias for {location beacon}
*	{aobject}	Alias for {object alike}
*	{female}	Alias for {name female}
*	{kobject}	Alias for {object known}
*	{male}		Alias for {name male}
* 	{placement}	Alias for {location placement}
*	{room}		Alias for {location room}
*
* Obfuscating information 
* A wildcard ended with a question mark will be replaced by an obfuscated
* value instead of the random value choosen by that wildcard, which will
* be included as metadata.
* The obfuscation is performed as follows (wildcards not shown can not
* be obfuscated):
*	{category}	The "objects" literal string
*	{location}	The "room" literal string for rooms, for placements and
*				beacons, the room to which the location belongs.
*	{object}	The category to which the object belongs.
*
* Comments
* C/C++/Java comments are permitted.
* In addition, any words, spaces, or marks after # or % or ; will be ignored
*
* Defining grammar name and tier
* The grammar name and tier may be set as a comment with the lines:
* ; grammar name MyGrammar
* ; grammar tier High
* Accepted values for the tier are Easy, Moderate, and High
*
*
*****************************************************************************/

; grammar name Cat3 - HRI and Incomplete Commands
; grammar tier High

$Main     = $polite $command

##############################################################################
#
# Complex commands
#
##############################################################################
$command  = $followob
$command  = $guideob
$command  = $findorob
$command  = $findmpob and $guidem
$command  = $findfpob and $guidef

$command  = $takeob and ($place | $deliver)
$command  = $take and ($placeob | $deliverob)
$command  = $takeob and ($placeob | $deliverob)

$command  = $vbplace {object?} on the {placement?}
$command  = $vbspeak something {void meta:$whattosay}

##############################################################################
#
# Tasks
#
##############################################################################
; Find object in room
$findor   = $vbgor to the {room}, $vbfind the {object}
$findor   = $vbfind a {object} in the {room}
$findorob = $vbfind a {object meta:Place it in the {room}}
$findorob = $vbfind a {category meta:A object} in the {room}

; Take an object
$take     = $vbtake the {aobject} from the {placement}
$takeob   = $vbtake the {aobject?} from the {placement?}

; Place an object
$place    = $vbplace it on the {placement}
$placeob  = $vbplace it on the {placement?}

; Deliver an object
$deliver  = $vbdeliver it to (me | {name} in the {beacon})
$deliverob= $vbdeliver it to {name? meta:Is at the {beacon} }

; Find people in location
$findmpob = $vbfind {male? meta:Is at the {beacon} }
$findfpob = $vbfind {female? meta:Is at the {beacon} }

; Find people in room
$findmpr  = $vbgor to the {room}, $vbfind {male}
$findmpr  = $vbfind {male} in the {room}
$findfpr  = $vbgor to the {room}, $vbfind {female}
$findfpr  = $vbfind {female} in the {room}

; Follow people
$followob = ($followm | $followf) {void meta:Person, {name}, is in the {beacon}}
$followm  = $vbfollow him
$followf  = $vbfollow her

; Guide people
$guideob  = $vbguide {name meta:Destination is {room}}
$guideob  = $vbguide {name?} to the ( {room} | {beacon} | exit )
$guidem   = $vbguide him to the ( {room} | {beacon} | exit )
$guidef   = $vbguide her to the ( {room} | {beacon} | exit )


##############################################################################
#
# What to say
#
##############################################################################
$whattosay = something about yourself
$whattosay = the time
$whattosay = what day is (today | tomorrow)
$whattosay = your team's (name | country | affiliation)
$whattosay = the day of the (week | month)
$whattosay = a joke

##############################################################################
#
# Verbs
#
##############################################################################
$vbtake   = get | grasp | take | pick up
$vbplace  = deliver | put | place
$vbspeak  = tell | say
$vbgopl   = go | navigate
$vbgor    = $vbgopl | enter
$vbfind   = find | locate | look for
$vbguide  = guide | escort | take
$vbfollow = follow
$vbdeliver = bring | deliver | give | hand

##############################################################################
#
# Polite
#
##############################################################################
$polite   = {void} | Please
$polite   = Could you
$polite   = Robot please
$polite   = Could you please


